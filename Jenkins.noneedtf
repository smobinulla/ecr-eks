# Initialize the AWS provider
provider "aws" {
  region = "ap-southeast-2"
}

# Generate a private key using the TLS provider
resource "tls_private_key" "jenkins_key" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

# Save the private key to a local file
resource "local_file" "private_key" {
  filename   = "${path.module}/jenkins-key.pem"
  content    = tls_private_key.jenkins_key.private_key_pem
  file_permission = "0600" # Secure file permissions for the key
}

# Create an AWS Key Pair using the public key
resource "aws_key_pair" "jenkins_key" {
  key_name   = "jenkins-key"
  public_key = tls_private_key.jenkins_key.public_key_openssh
}

# Define a Security Group for Jenkins
resource "aws_security_group" "jenkins_sg" {
  name        = "jenkins-sg"
  description = "Allow SSH and HTTP for Jenkins"

  ingress {
    description = "Allow SSH"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    description = "Allow HTTP"
    from_port   = 8080
    to_port     = 8080
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Define an EC2 instance for Jenkins
resource "aws_instance" "jenkins" {
  ami           = "ami-001f2488b35ca8aad" # Replace with a region-specific Ubuntu AMI
  instance_type = "t2.micro"

  key_name        = aws_key_pair.jenkins_key.key_name
  security_groups = [aws_security_group.jenkins_sg.name]

  tags = {
    Name = "Jenkins-Server"
  }

  user_data = <<-EOT
#!/bin/bash
# Update and install required packages
sudo apt update -y
sudo apt upgrade -y
sleep 1m
sudo apt install -y openjdk-17-jdk wget
sleep 1m
# Install Jenkins
sudo wget -O /usr/share/keyrings/jenkins-keyring.asc \
  https://pkg.jenkins.io/debian-stable/jenkins.io-2023.key
echo "deb [signed-by=/usr/share/keyrings/jenkins-keyring.asc]" \
  https://pkg.jenkins.io/debian-stable binary/ | sudo tee \
  /etc/apt/sources.list.d/jenkins.list > /dev/null
sudo apt-get update -y
sudo apt-get install jenkins -y
# Start Jenkins
sudo systemctl start jenkins
sudo systemctl enable jenkins
EOT
}

# Output Jenkins public IP and private key file path
output "jenkins_public_ip" {
  value = aws_instance.jenkins.public_ip
}

output "private_key_path" {
  value = local_file.private_key.filename
  description = "Path to the private key file to SSH into the Jenkins server"
}